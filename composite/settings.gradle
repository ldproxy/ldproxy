pluginManagement {
    repositories {
        gradlePluginPortal()
        maven {
            url "https://dl.interactive-instruments.de/repository/maven-releases/"
        }
        maven {
            url "https://dl.interactive-instruments.de/repository/maven-snapshots/"
        }
    }
}

buildscript {
    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
            cacheDynamicVersionsFor 0, 'seconds'
        }
    }
}

plugins {
    id "de.interactive_instruments.xtraplatform-settings" version "6.1.0-SNAPSHOT"
}

rootProject.name = 'ldproxy-composite'

def siblingDirectories = rootDir.parentFile.parentFile.listFiles()
def xtraplatformBuild = siblingDirectories.find({ it.name.toLowerCase().equals('xtraplatform-build') })
def xtraplatformCore = siblingDirectories.find({ it.name.toLowerCase().equals('xtraplatform') })
def xtraplatformSpatial = siblingDirectories.find({ it.name.toLowerCase().equals('xtraplatform-spatial') })
def xtraplatformNative = siblingDirectories.find({ it.name.toLowerCase().equals('xtraplatform-native') })

if (hasProperty('build-plugins') && xtraplatformBuild != null) {
    includeBuild "../../${xtraplatformBuild.name}"
}

def includes = []

if (xtraplatformCore != null) includes.add('xtraplatform-core');
if (xtraplatformNative != null && hasProperty('native')) includes.add('xtraplatform-native');
if (xtraplatformSpatial != null) includes.add('xtraplatform-spatial');

apply from: '../shared.gradle'

xtraplatform {
    includeOnlyIf { layer -> layer.name == 'ldproxy' || includes.contains(layer.name) }

    layerInclude       name: 'xtraplatform-core', group: 'de.interactive_instruments', version: coreVersion, path: '../../', dir: 'xtraplatform'
    layerIncludeNative name: 'xtraplatform-native', group: 'de.interactive_instruments', version: nativeVersion, path: '../../'
    layerInclude       name: 'xtraplatform-spatial', group: 'de.interactive_instruments', version: spatialVersion, path: '../../'

    layerInclude       name: 'ldproxy', group: 'de.interactive_instruments', path: '../../', dir: rootDir.parentFile.name
}
