pluginManagement {
    repositories {
        gradlePluginPortal()
        maven {
            url "https://dl.interactive-instruments.de/repository/maven-releases/"
        }
        maven {
            url "https://dl.interactive-instruments.de/repository/maven-snapshots/"
        }
    }
}

buildscript {
    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
            cacheDynamicVersionsFor 0, 'seconds'
        }
    }
}

plugins {
    id "de.interactive_instruments.xtraplatform-settings" version "6.1.0-SNAPSHOT"
}

apply from: '../shared.gradle'

rootProject.name = 'ldproxy-composite'

def siblingDirectories = rootDir.parentFile.parentFile.listFiles()
def xtraplatformBuild = siblingDirectories.find({ it.name.toLowerCase().equals('xtraplatform-build') })
def xtraplatformCore = siblingDirectories.find({ it.name.toLowerCase().equals('xtraplatform') })
def xtraplatformSpatial = siblingDirectories.find({ it.name.toLowerCase().equals('xtraplatform-spatial') })
def xtraplatformNative = siblingDirectories.find({ it.name.toLowerCase().equals('xtraplatform-native') })

if (hasProperty('build-plugins') && xtraplatformBuild) {
    includeBuild "../../${xtraplatformBuild.name}"
}

xtraplatform {    
    if (xtraplatformCore) {
        layerInclude path: '../../', dir: "${xtraplatformCore.name}", group: 'de.interactive_instruments', name: 'xtraplatform-core'
    }
    if (xtraplatformSpatial) {
        layerInclude path: '../../', dir: "${xtraplatformSpatial.name}", group: 'de.interactive_instruments', name: 'xtraplatform-spatial'
    }
    if (hasProperty('native') && xtraplatformNative) {
        //TODO
        //layerIncludeNative path: '../../', dir: "${xtraplatformSpatial.name}", group: 'de.interactive_instruments', name: 'xtraplatform-spatial'
    }

    layerInclude path: '../../', dir: rootDir.parentFile.name, group: 'de.interactive_instruments', name: 'ldproxy'

    exclude 'ldproxy-cfg'
}
